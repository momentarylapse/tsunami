// Image = hui:copy
// Title[Deutsch] = Flanger

use "../../api.kaba"

bool not_first_time


window FlangerDialog

class PluginData
	float Delay
	float Speed
	float Depth

PluginData data

void ResetData()
	data.Depth = 0.5
	data.Speed = 0.3
	data.Delay = 0.01

class PluginState
	float temp_r[]
	float temp_l[]
	int offset

PluginState state

void ResetState()
	state.temp_r.clear()
	state.temp_l.clear()
	state.offset = 0

void OnSpeed()
	data.Speed = SliderGet(FlangerDialog, "speed")

void OnDepth()
	data.Depth = SliderGet(FlangerDialog, "depth")

void OnDelay()
	data.Delay = SliderGet(FlangerDialog, "delay")

void DoFlanger(float b[], float temp[], int sample_rate)
	int p = state.offset % temp.num
	
	float f_a = (1 - 0.5 * data.Depth) * data.Delay
	float f_b = 0.5 * data.Depth * data.Delay
	float f_c = 1.0 / sample_rate * 2 * pi * data.Speed
	
	for int i, 0, b.num
	
		// old value
		temp[p] = b[i]
		
		// shift
		float dt = f_a - cos((i + state.offset) * f_c) * f_b
		int dp = dt * sample_rate
		int p2 = p - dp
		if p2 < 0
			p2 += temp.num
		
		// add echo
		b[i] += temp[p2]
		
		p ++
		if p >= temp.num
			p = 0

void DataToDialog()
	SliderSet(FlangerDialog, "speed", data.Speed)
	SliderSet(FlangerDialog, "depth", data.Depth)
	SliderSet(FlangerDialog, "delay", data.Delay)

void Configure()
	// dialog
	FlangerDialog = new FixedDialog("Flanger",485,175,MainWin,false)
	FlangerDialog.AddSlider("",115,35,205,25,"speed_slider")
	FlangerDialog.AddSpinButton("\\0\\10000\\0.1",325,35,130,25,"speed")
	FlangerDialog.AddText("Hz",460,35,30,25,"")
	FlangerDialog.AddSlider("",115,65,205,25,"delay_slider")
	FlangerDialog.AddSpinButton("\\0\\10000\\0.1",325,65,130,25,"delay")
	FlangerDialog.AddText("ms",460,65,30,25,"")
	FlangerDialog.AddSlider("",115,95,205,25,"depth_slider")
	FlangerDialog.AddSpinButton("\\0\\100\\0.1",325,95,130,25,"depth")
	FlangerDialog.AddText("%",460,95,30,25,"")
	FlangerDialog.AddText("Geschwindigkeit",5,35,110,25,"")
	FlangerDialog.AddText("Verz√∂gerung",5,65,100,25,"")
	FlangerDialog.AddText("Tiefe",5,95,100,25,"")
	PutCommandBarFixed(FlangerDialog, 5, 135, 485)
	
	AddSlider(FlangerDialog, "speed_slider", "speed", 0, 2,    1,    &OnSpeed, data.Speed)
	AddSlider(FlangerDialog, "depth_slider", "depth", 0, 1,    100,  &OnDepth, data.Depth)
	AddSlider(FlangerDialog, "delay_slider", "delay", 0, 0.02, 1000, &OnDelay, data.Delay)
	
	PutFavoriteBarFixed(FlangerDialog, 5, 5, 485)
	
	DataToDialog()
	
	FlangerDialog.Run()
	
void ProcessTrack(BufferBox buf)
	AudioFile *a = audio
	int delay_samples = a.sample_rate * data.Delay + 1
	state.temp_r.resize(delay_samples)
	state.temp_l.resize(delay_samples)
	
	DoFlanger(buf.r, state.temp_r, a.sample_rate)
	DoFlanger(buf.l, state.temp_l, a.sample_rate)
	
	state.offset += buf.num

